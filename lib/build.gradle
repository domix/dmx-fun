plugins {
    // Apply the groovy plugin to also add support for Groovy (needed for Spock)
    id 'groovy'

    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'jacoco'
    id("com.github.ben-manes.versions") version "0.53.0"
    id "com.vanniktech.maven.publish" version "0.35.0"
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use the latest Groovy version for Spock testing
    testImplementation libs.groovy

    // Use the awesome Spock testing and specification framework even with Java
    testImplementation libs.spock.core

    testImplementation(platform('org.junit:junit-bom:6.0.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}

mavenPublishing {
    publishToMavenCentral(true, false)
    signAllPublications()
    coordinates("codes.domix", "fun", "0.0.2")

    pom {
        name = 'Functional Programming Constructions in Java.'
        description = 'The goal is to demonstrate how functional paradigms—such as immutability, pure functions, higher-order functions, currying, and more—can be expressed in Java, a traditionally object-oriented language.'
        inceptionYear = "2025"
        url = 'https://fun.domix.codes'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'domix'
                name = 'Domingo Suarez Torres'
                email = 'domingo.suarez@gmail.com'
            }
        }
        scm {
            connection = 'scm:git:https://github.com/domix/dmx-fun'
            developerConnection = 'scm:git:https://github.com/domix/dmx-fun'
            url = 'https://github.com/domix/dmx-fun'
        }
    }
}
